buildscript {
    ext.vaadin8_version = '8.5.2'
    ext.karibudsl_version = '0.4.10'
    ext.kaributesting_version = '0.4.17'
    ext.vaadin10_version = '11.0.1'
    ext.hikaricp_version = '3.2.0'
    ext.flyway_version = '5.2.0'
    ext.dynatest_version = "0.9"
    ext.logback_version = "1.2.3"
    ext.h2_version = "1.4.197"
    ext.slf4j_version = "1.7.25"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.71"
    id "org.gretty" version "2.2.0"
    id "com.jfrog.bintray" version "1.8.1"
}

repositories {
    jcenter()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.vaadinonkotlin'
    version '0.5.1-SNAPSHOT'

    // Heroku
    task stage(dependsOn: ['build'])
}

subprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            // to see the exceptions of failed tests in Travis-CI console.
            exceptionFormat 'full'
        }
    }
}
