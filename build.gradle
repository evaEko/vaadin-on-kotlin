buildscript {
    ext.vaadin8_version = '8.5.2'
    ext.karibudsl_version = '0.4.9'
    ext.kaributesting_version = '0.4.16'
    ext.vaadin10_version = '10.0.5'
    ext.hikaricp_version = '3.2.0'
    ext.flyway_version = '5.1.4'
    ext.dynatest_version = "0.9"
    ext.logback_version = "1.2.3"
    ext.h2_version = "1.4.197"
    ext.slf4j_version = "1.7.25"

    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.61"
    id "org.gretty" version "2.1.0"
    id "com.jfrog.bintray" version "1.8.1"
}

repositories {
    mavenCentral()
    jcenter()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.vok'
    version '0.4.15'

    // Heroku
    task stage(dependsOn: ['build'])
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        maven { url 'https://dl.bintray.com/mvysny/github' }
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            // to see the exceptions of failed tests in Travis-CI console.
            exceptionFormat 'full'
        }
    }
}
