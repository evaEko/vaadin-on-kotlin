apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

dependencies {
    compile(project(':vok-framework'))
    compile(project(':vok-security'))

    // Vaadin
    compile "com.github.vok.karibudsl:karibu-dsl-v8:$karibudsl_version"
    compile "com.vaadin:vaadin-server:$vaadin8_version"
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // IDEA language injections
    compile 'com.intellij:annotations:12.0'

    // testing
    testCompile("com.github.mvysny.dynatest:dynatest:${dynatest_version}")
    testCompile "com.github.kaributesting:karibu-testing-v8:$kaributesting_version"
    testCompile "ch.qos.logback:logback-classic:$logback_version"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-util-vaadin8'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'VOK: Basic utility classes for Vaadin 8')
                root.appendNode('name', 'vok-util-vaadin8')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'vaadin-on-kotlin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}

