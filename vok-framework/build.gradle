apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: "com.jfrog.bintray"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    testCompile("com.github.mvysny.dynatest:dynatest:${dynatest_version}")
    testCompile "ch.qos.logback:logback-classic:$logback_version"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'vok-framework'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'VoK: The Core classes')
                root.appendNode('name', 'vok-framework')
                root.appendNode('url', 'https://github.com/mvysny/vaadin-on-kotlin')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'github'
        name = 'vaadin-on-kotlin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mvysny/vaadin-on-kotlin'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
